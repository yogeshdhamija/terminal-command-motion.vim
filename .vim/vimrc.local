let g:terminal_command_motion_prompt_matcher = "^yogeshdhamija in"

function! FindPreviousPrompt() abort
    let l:oldWrapscan = &wrapscan
    let l:oldSearch = @/

    set nowrapscan
    let @/ = g:terminal_command_motion_prompt_matcher

    let l:oldPosition = getpos(".")
    silent! normal! N
    if(getpos(".") == l:oldPosition)
        silent! normal! gg0
    endif

    let @/ = l:oldSearch
    let &wrapscan = l:oldWrapscan
endfunction

function! FindNextPrompt() abort
    let l:oldWrapscan = &wrapscan
    let l:oldSearch = @/
    let l:endOfFile = 0

    set nowrapscan
    let @/ = g:terminal_command_motion_prompt_matcher

    let l:oldPosition = getpos(".")
    silent! normal! n
    if(getpos(".") == l:oldPosition)
        silent! normal! G$
        let l:endOfFile = 1
    endif

    let @/ = l:oldSearch
    let &wrapscan = l:oldWrapscan
    return l:endOfFile
endfunction

function! SelectAllCommand() abort
    " In case you're at the beginning of a prompt already
    " let's move you to the end of the line, so finding 
    " the previous prompt will result in your current one, and 
    " not the one before
    normal! o$o$

    normal! o
    call FindPreviousPrompt()
    normal! o

    let l:endOfFile = FindNextPrompt()

    " So the first character of the next prompt
    " is not included in the selection
    if(!l:endOfFile)
        normal! k$
    endif
endfunction

nnoremap cm <Cmd>call FindNextPrompt()<CR>
nnoremap CM <Cmd>call FindPreviousPrompt()<CR>

vnoremap cm <Cmd>call FindNextPrompt()<CR>
vnoremap CM <Cmd>call FindPreviousPrompt()<CR>

vnoremap acm <Cmd>call SelectAllCommand()<CR>
